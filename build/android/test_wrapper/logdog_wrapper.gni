# Copyright 2024 The Chromium Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/util/generate_wrapper.gni")

# Defines a generated_wrapper script test under bin/ in the build dir that is
# itself wrapped by the logdog_wrapper.py tool, which will automatically stream
# the device's logcats to logdog.
template("logdog_wrapper_script_test") {
  forward_variables_from(invoker,
                         [
                           "data_deps",
                           "args",
                         ])
  generate_wrapper(target_name) {
    executable = "../../build/android/test_wrapper/logdog_wrapper.py"
    wrapper_script = "$root_out_dir/bin/run_${target_name}"
    testonly = true

    if (!defined(args)) {
      args = []
    }
    executable_args = args

    if (!defined(data_deps)) {
      data_deps = []
    }
    data_deps += [ "//build/android:logdog_wrapper_py" ]
  }
}
